group 'commercehub'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration/java')
        }
        resources.srcDir file('src/integration/resources')
    }
    functionalTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functional/groovy')
        }
        resources.srcDir file('src/functional/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

dependencies {
    testCompile (
        'junit:junit:4.11',
        'org.codehaus.groovy:groovy-all:2.4.7',
        'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
    )

    functionalTestRuntime (
        'com.athaydes:spock-reports:1.2.13'
    )
}



configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime
}


